grammar de.nordakademie.ticket.Ticket with org.eclipse.xtext.common.Terminals

generate ticket "http://www.nordakademie.de/ticket/Ticket"


//Kommentar mit ä oder ö
//Zweiter Kommentar mit ä oder ö unter utf-8

ModelIssue:
	(	person += Person | 
		role += Role |
		transition += Transition |
		issueType += IssueType |
		workflow += Workflow |
		fields += Field |
		status += Status 
	)*
	issueScreen = IssueScreen
	
	((	person += Person | 
		role += Role |
		transition += Transition |
		issueType += IssueType |
		workflow += Workflow |
		status += Status
	)*)?
		
;

IssueScreen: 	
	'IssueScreen' name=ID '{'
		statusfield = StatusField
		(assigneefield = AssigneeField) ?
		summaryfield = SummaryField
		fields += Field*
	'}'
;

Person:
	'Person' name = ID '{' 
		roles += [Role]* 
	'}'
;

Role:
	'Role' name = ID '{' 
		transitions += [Transition]* 
	'}'
;

Transition:
	'Transition' name = ID 'named' title = STRING '{'
		start = [Status] 'to' ziel = [Status] 
	'}'
;

IssueType:
	'IssueType' name = ID 'follows' workflow = [Workflow] '{'
		fields += [Field]*
	'}'
;

Workflow:
	'Workflow' name = ID '{' 
		transitions += [Transition]* 
	'}'
;

Status:
	'Status' name=ID
;


StatusField:
	'StatusField' name = ID '{'
		( 'default' default = [Status] ) ?
	'}'
;

AssigneeField:
	'AssigneeField' name = ID '{'
		( 'default' default = [Person] ) ?
	'}'
;

SummaryField: 
	'SummaryField' name = ID '{' 
		( 'default' default = STRING ) ?
	'}'
;

Field:
	DateField | StringField | CheckField | MailField | ComboField
;



MailField:
	'MailField' name = ID 'as' description = STRING '{'
		( 'default' default = STRING ) ?
	'}'
;

DateField:
	'DateField' name = ID 'as' description = STRING '{'
		( ('default' today?= 'today') | ('default' default = Date )) ?
	'}'
;

Date:
	day = INT '.' month = INT '.' year = INT
;

StringField:
	'StringField' name = ID 'as' description = STRING '{'
		( 'default' default = STRING )?
	'}'
;

ComboField:
	'ComboField' name = ID 'as' description = STRING '{'
		('default' '{' default += STRING* '}' )?
	'}'
;

CheckField:
	'CheckField' name = ID 'as' description = STRING '{'
		( ('default' default ?= 'TRUE' ) | ('default' 'FALSE' ) )?
	'}'
;

