grammar de.nordakademie.ticket.Ticket with org.eclipse.xtext.common.Terminals

generate ticket "http://www.nordakademie.de/ticket/Ticket"


//Kommentar mit ä oder ö
//Zweiter Kommentar mit ä oder ö unter utf-8

ModelIssue:
	(	person += Person | 
		role += Role |
		transition += Transition |
		issueType += IssueType |
		workflow += Workflow |
		fields += Field |
		status += Status 
	)*
	issueScreen = IssueScreen
	
	(	person += Person | 
		role += Role |
		transition += Transition |
		issueType += IssueType |
		workflow += Workflow |
		fields += Field |
		status += Status
	)*
		
;

IssueScreen: 	
	'IssueScreen' name=ID '{'
		statusfield = StatusField
		summaryfield = SummaryField
		fields += [Field]*
	'}'
;

Person:
	'Person' name = ID 'called' shownName = STRING '{' 
		roles += [Role]* 
	'}'
;

Role:
	'Role' name = ID '{' 
		( openIssue ?= 'create Issue' ) ?
		transitions += [Transition]* 
	'}'
;

Transition:
	'Transition' name = ID 'named' title = STRING '{'
		start = [Status] 'to' ziel = [Status] 
	'}'
;

IssueType:
	'IssueType' name = ID 'follows' workflow = [Workflow] '{'
		fields += [Field]*
	'}'
;

Workflow:
	'Workflow' name = ID '{' 
		transitions += [Transition]+
	'}'
;

Status:
	'Status' name=ID
;


StatusField:
	'StatusField' name = ID '{'
		( 'default' default = [Status] ) ?
	'}'
;

SummaryField: 
	'SummaryField' name = ID '{' 
		( 'default' default = STRING ) ?
	'}'
;

Field:
	DateField | StringField | CheckField | MailField | ComboField | PersonField
;



MailField:
	'MailField' name = ID 'as' description = STRING '{'
		( 'default' default = MAIL ) ?
	'}'
;


DateField:
	'DateField' name = ID 'as' description = STRING '{'
		( ('default' today?= 'today') | ('default' default = Date )) ?
	'}'
;

Date:
	day = INT '.' month = INT '.' year = INT
;

StringField:
	'StringField' name = ID 'as' description = STRING '{'
		( 'default' default = STRING )?
	'}'
;

ComboField:
	'ComboField' name = ID 'as' description = STRING '{'
		('default' '{' ( default += STRING ',' )*  default += STRING '}' )?
	'}'
;

CheckField:
	'CheckField' name = ID 'as' description = STRING '{'
		( ('default' default ?= 'TRUE' ) | ('default' 'FALSE' ) )?
	'}'
;


PersonField:
	'PersonField' name = ID  'as' description = STRING '{'
		( 'default' default = [Person] ) ?
	'}'
;

terminal MAIL:
	( 'a'..'z'|'A'..'Z'|'0'..'9'|'.'|'-'|'_'|'%' ) +     	//'beispiel123'
	'@' ('a'..'z'|'A'..'Z'|'0'..'9'|'.'|'-')+				//'@wak-sh'
	'.' ('a'..'z' | 'A'..'Z')('a'..'z' | 'A'..'Z')+			//'.de'
;