grammar de.nordakademie.ticket.Ticket with org.eclipse.xtext.common.Terminals

generate ticket "http://www.nordakademie.de/ticket/Ticket"


//Kommentar mit ä oder ö
//Zweiter Kommentar mit ä oder ö unter utf-8

ModelIssue:
	(	issue += Issue | 
		person += Person | 
		role += Role |
		transition += Transition |
		issueType += IssueType |
		workflow += Workflow |
		status += Status 
	)*
;

Issue: 	
	'Issue' name=ID 'of' issueType = [IssueType] '{'
		'status' status = [Status]
		('from' assignee = [Person]) ?
		summary = STRING
	'}'
;

Person:
	'Person' name = ID '{' 
		roles += [Role]* 
	'}'
;

Role:
	'Role' name = ID '{' 
		transitions += [Transition]* 
	'}'
;

Transition:
	'Transition' name = ID 'named' title = STRING '{'
		start = [Status] 'to' ziel = [Status] 
	'}'
;

IssueType:
	'IssueType' name = ID 'follows' workflow = [Workflow] '{'
		fields += Field*
	'}'
;

Workflow:
	'Workflow' name = ID '{' 
		transitions += [Transition]* 
	'}'
;

Status:
	'Status' name=ID
;

Field:
	DateField | StringField | ComboField | CheckField
;

DateField:
	'Date' name = ID '{'
	content = Date
	'}'
;

Date:
	day = INT '.' month = INT '.' year = INT
;

StringField:
	'String' name = ID '{'
	string = STRING
	'}'
;

ComboField:
	'Combo' name = ID '{'
		'entries' '{' entries += STRING* '}'
		'index' index = INT
	'}'
;

CheckField:
	'Check' name = ID '{'
		text = STRING
		( checked ?= 'x' )?
	'}'
;

